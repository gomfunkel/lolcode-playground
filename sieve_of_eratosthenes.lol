HAI 1.3

  OBTW
    This is a LOLCODE implementation of the Sieve of Eratosthenes to find prime
    numbers.

    Further information about the sieve can be found here:
    http://en.wikipedia.org/wiki/Sieve_of_Eratosthenes

    This code is using BUKKITS as proposed in LOLCODE 1.3 and implemented in
    the LOLCODE interpreter LCI by Justin Meza (future branch):
    http://www.icanhaslolcode.org
  TLDR

  BTW O HAI, IM IN UR NUMBRS SIEVING UR PRIMEZ LIEK ERATOSTHENES

  I HAS A MAXIMUM ITZ 100
  I HAS A BUKKITOFNUMBRS ITZ A BUKKIT
  I HAS A POINTR ITZ 0
  I HAS A GOAWAEY ITZ 0

  BTW INITUALIZING UR BUKKIT
  IM IN YR NUMBRS UPPIN YR COUNTR WILE BOTH SAEM COUNTR AN SMALLR OF COUNTR AN DIFF OF MAXIMUM AN 1
    POINTR R SUM OF COUNTR AN 1
    BUKKITOFNUMBRS HAS A SRS POINTR ITZ WIN
  IM OUTTA YR NUMBRS
  BUKKITOFNUMBRS'Z SRS 1 R FAIL

  BTW LOOKIN FO PRIMEZ
  IM IN YR NUMBRS UPPIN YR COUNTR WILE BOTH SAEM COUNTR AN SMALLR OF COUNTR AN DIFF OF MAXIMUM AN 1
    POINTR R SUM OF COUNTR AN 1
    BTW IS DIS NUMBR MEBBE PRIME?
    BUKKITOFNUMBRS'Z SRS POINTR, O RLY?
      YA RLY
        BTW ALL MULTIPLES OF DIS NUMBR CAN NOT IS PRIME
        IM IN YR NUMBRSAGAIN UPPIN YR MULTIPLIR WILE BOTH SAEM GOAWAEY AN SMALLR OF GOAWAEY AN DIFF OF MAXIMUM AN 1
          GOAWAEY R SUM OF POINTR AN PRODUKT OF SUM OF MULTIPLIR AN 1 AN POINTR
          BOTH SAEM GOAWAEY AN SMALLR OF GOAWAEY AN MAXIMUM, O RLY?
            YA RLY
              BUKKITOFNUMBRS'Z SRS GOAWAEY R FAIL
          OIC
        IM OUTTA YR NUMBRSAGAIN
        GOAWAEY R 0
    OIC
  IM OUTTA YR NUMBRS

  BTW IMA SHOW U TEH PRIMEZ
  IM IN YR PRIMEZ UPPIN YR COUNTR WILE BOTH SAEM COUNTR SMALLR OF COUNTR AN DIFF OF MAXIMUM AN 1
    POINTR R SUM OF COUNTR AN 1
    BUKKITOFNUMBRS'Z SRS POINTR, O RLY?
      YA RLY
        VISIBLE SMOOSH POINTR AN " IZ PRIME!" MKAY
    OIC
  IM OUTTA YR PRIMEZ

KTHXBYE